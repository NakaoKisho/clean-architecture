plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.android.libraries.mapsplatform.secrets-gradle-plugin'
    id 'kotlin-kapt'
    id 'com.google.dagger.hilt.android'
    // Hilt
    id 'dagger.hilt.android.plugin'
    // Proto DataStore
    id "com.google.protobuf" version "0.9.1"
}

android {
    namespace 'com.vegcale.architecture'
    compileSdk 35

    defaultConfig {
        applicationId "com.vegcale.architecture"
        minSdk 29
        targetSdk 35
        versionCode 1
        versionName "1.0"

        def properties = new Properties()
        properties.load(project.rootProject.file("local.properties").newDataInputStream())
        def EARTHQUAKE_API_URL = properties.getProperty("EARTHQUAKE_API_URL")
        buildConfigField("String", "EARTHQUAKE_API_URL", "\"${EARTHQUAKE_API_URL}\"")

//        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunner "com.vegcale.architecture.CustomTestRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
        freeCompilerArgs = ["-Xcontext-receivers"]
    }
    buildFeatures {
        compose true
        buildConfig true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.4.2'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    sourceSets {
        test {
            resources.srcDirs += ['src/test/res']
        }
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.12.0'
    implementation platform('org.jetbrains.kotlin:kotlin-bom:1.9.0')
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.2'
    implementation 'androidx.activity:activity-compose:1.7.2'
    implementation platform('androidx.compose:compose-bom:2023.06.01')
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-graphics'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    implementation "androidx.compose.material:material:1.6.0-alpha05"
    implementation 'androidx.compose.material3:material3:1.2.0-alpha07'
    implementation 'com.google.android.gms:play-services-location:21.0.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation platform('androidx.compose:compose-bom:2023.06.01')
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4'
    debugImplementation 'androidx.compose.ui:ui-tooling'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'

    // AnchoredDraggable
    implementation("androidx.compose.foundation:foundation:1.6.0-alpha05")

    // Google Map
    implementation 'com.google.maps.android:maps-compose:2.12.0'
    implementation "com.google.android.gms:play-services-maps:18.1.0"

    // Coil
//    implementation "io.coil-kt:coil-compose:2.4.0"

    // Coroutine
    def coroutines_version = '1.7.3'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    // Hilt
    def hilt_version = '2.47'
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"
    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0'
    implementation 'androidx.hilt:hilt-work:1.0.0'
    kapt 'androidx.hilt:hilt-compiler:1.0.0'

    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'

    implementation "androidx.lifecycle:lifecycle-runtime-compose:2.6.2"

    // Navigation
    def nav_version = "2.7.3"
    implementation "androidx.navigation:navigation-compose:$nav_version"
    // Navigation Test
    androidTestImplementation "androidx.navigation:navigation-testing:$nav_version"

    // Hilt Test
    androidTestImplementation("com.google.dagger:hilt-android-testing:$hilt_version")
    kaptAndroidTest("com.google.dagger:hilt-android-compiler:$hilt_version")

    // Coroutines Test
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"

    // MockWebServer
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.9.2'

    // File Read
    testImplementation "commons-io:commons-io:2.6"

    // Kotlinx Datetime
    implementation "org.jetbrains.kotlinx:kotlinx-datetime:0.4.0"

    // WorkManager
    def work_version = "2.8.1"
    implementation("androidx.work:work-runtime-ktx:$work_version")

    // Proto DataStore
    implementation  "androidx.datastore:datastore:1.1.0-alpha05"
    implementation  "com.google.protobuf:protobuf-javalite:3.24.0"

    // Admob
    implementation "com.google.android.gms:play-services-ads:22.4.0"
}

kapt {
    correctErrorTypes true
}

// Proto DataStore
protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.24.3"
    }

    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option 'lite'
                }
            }
        }
    }
}